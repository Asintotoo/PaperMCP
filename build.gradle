plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id 'com.gradleup.shadow' version '8.3.1'
}

group = 'com.asintoto'
version = '1.0.0-BETA-1'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.8-R0.1-SNAPSHOT")
    compileOnly('org.projectlombok:lombok:1.18.34')
    implementation('io.github.revxrsal:lamp.common:4.0.0-rc.11')
    implementation('io.github.revxrsal:lamp.bukkit:4.0.0-rc.11')
    implementation('io.github.codeboyzhou:mcp-declarative-java-sdk:0.8.3')
    implementation('io.modelcontextprotocol.sdk:mcp-json-jackson2:0.13.0')
    annotationProcessor('org.projectlombok:lombok:1.18.34')
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21")
    }
}

tasks.assemble {
    dependsOn(tasks.shadowJar)
}

tasks.shadowJar {
    archiveClassifier.set("")
    relocate "revxrsal", "com.asintoto.libs.revxrsal"

    mergeServiceFiles()
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

compileJava {
    // Preserve parameter names in the bytecode
    options.compilerArgs += ["-parameters"]
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
